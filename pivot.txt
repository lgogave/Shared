ALTER PROCEDURE [dbo].[GetSchAdhFrequentDefaulterAgents]
    @Month SMALLINT = NULL,
    @Year SMALLINT = NULL,
    @LOB VARCHAR(MAX) = NULL,
    @Location VARCHAR(MAX) = NULL,
    @Superwisor VARCHAR(MAX) = NULL,
    @Agent VARCHAR(MAX) = NULL,
    @ProdStatus VARCHAR(MAX) = NULL
AS
BEGIN
    DECLARE @SQL NVARCHAR(MAX) = ''
    DECLARE @Params NVARCHAR(MAX) = N'@Month SMALLINT, @Year SMALLINT, @LOB VARCHAR(MAX), @Location VARCHAR(MAX), @Superwisor VARCHAR(MAX), @Agent VARCHAR(MAX), @ProdStatus VARCHAR(MAX)'

    SET @SQL = '
        SELECT TOP 20
            EmpId,
            Name,
            COUNT(*) AS ViolationCount,
            Location,
            LOB,
            Supervisor,
            Role
        FROM 
            [dbo].TranSchAdhWex
        WHERE ReportMonth = @Month AND ReportYear = @Year
            AND (' + CASE WHEN @LOB IS NOT NULL THEN 'LOB IN (SELECT CAST(Item AS NVARCHAR(MAX)) FROM dbo.SplitString(@LOB, '',''))' ELSE '1 = 1' END + ')
            AND (' + CASE WHEN @Location IS NOT NULL THEN '[Location] IN (SELECT CAST(Item AS NVARCHAR(MAX)) FROM dbo.SplitString(@Location, '',''))' ELSE '1 = 1' END + ')
            AND (' + CASE WHEN @Superwisor IS NOT NULL THEN '[Supervisor] IN (SELECT CAST(Item AS NVARCHAR(MAX)) FROM dbo.SplitString(@Superwisor, '',''))' ELSE '1 = 1' END + ')
            AND (' + CASE WHEN @Agent IS NOT NULL THEN '[Name] IN (SELECT CAST(Item AS NVARCHAR(MAX)) FROM dbo.SplitString(@Agent, '',''))' ELSE '1 = 1' END + ')
            AND (' + CASE WHEN @ProdStatus IS NOT NULL THEN '[Production Status] IN (SELECT CAST(Item AS NVARCHAR(MAX)) FROM dbo.SplitString(@ProdStatus, '',''))' ELSE '1 = 1' END + ')
            AND Scheduled_Duration > 0
            AND (Actual_Duration / Scheduled_Duration * 100) < 80
        GROUP BY 
            EmpId, Name, Location, LOB, Supervisor, Role
        HAVING 
            COUNT(*) > 3
        ORDER BY 
            ViolationCount DESC'

    EXEC sp_executesql @SQL, @Params, @Month = @Month, @Year = @Year, @LOB = @LOB, @Location = @Location, @Superwisor = @Superwisor, @Agent = @Agent, @ProdStatus = @ProdStatus
END
